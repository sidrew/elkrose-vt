## VT overlay - floats cherry widgets over base game screens without overriding them
init python:
    config.overlay_screens.append("vt_cherry_overlay")

# Labels whose conversation menu is addressed to the mother (talker = mother) even
# though selected_girl stays the daughter. Used by the convo_menu cherry below.
define vt_mother_convo_labels = {"home_visit_mother_discussion"}

# Last label reached at runtime, so the overlay can tell which conversation it is
# floating over. Set by the chained config.label_callback installed below.
default vt_active_label = ""

# Sex-act history per girl (id -> {"act", "raw"}) for the penetration->oral reaction. Two slots because the
# recorder runs before the oral hook: vt_last_act_by_girl = most recent act; vt_prev_act_by_girl = the one
# before it (what vt_catch_oral_sex reads). Reset each encounter below.
default vt_prev_act_by_girl = {}
default vt_last_act_by_girl = {}

# Girl ids who conceded "finish inside" this encounter only (03_vt_pullout_coercion.rpy). Permanent
# version = the girl's vt_accepts_vaginal_creampie flag.
default vt_creampie_session = set()

# Chained label_callback (runs late to wrap base/other-mod callbacks). Underscore name = not saved.
init 100 python:
    def _vt_label_callback(name, abnormal):
        store.vt_active_label = name
        # New sex session -> clear per-session go-bare state (concessions revert, attempts reset).
        if name == "start_sex_interaction":
            try:
                vt_reset_gobare_session()
            except Exception:
                pass
            # Fresh encounter -> no "previous act" yet (used by the penetration->oral reaction).
            try:
                store.vt_prev_act_by_girl.clear()
                store.vt_last_act_by_girl.clear()
            except Exception:
                store.vt_prev_act_by_girl = {}
                store.vt_last_act_by_girl = {}
            # Fresh encounter -> this-session "finish inside" concessions revert.
            try:
                store.vt_creampie_session.clear()
            except Exception:
                store.vt_creampie_session = set()
        _prev = getattr(store, "_vt_prev_label_callback", None)
        if _prev is not None:
            _prev(name, abnormal)

    if not getattr(store, "_vt_label_callback_installed", False):
        store._vt_prev_label_callback = config.label_callback
        config.label_callback = _vt_label_callback
        store._vt_label_callback_installed = True

transform vt_appear_after_popup:
    alpha 0.0
    pause 0.5
    alpha 1.0

screen vt_cherry_overlay():
    zorder 50
    modal False

    $ _vt_girl    = getattr(store, "selected_girl", None)
    $ _vt_part    = getattr(store, "participant", None)
    $ _vt_tooltip = GetTooltip()

    # Girl quick overview popup - takes priority so it replaces any exam/sex cherry
    if renpy.get_screen("girl_quick_overview"):
        $ _vt_qo = renpy.get_screen("girl_quick_overview")
        $ _vt_qo_girl = _vt_qo.scope.get("girl") if _vt_qo else None
        if _vt_qo_girl:
            # Absolute positions — tuned per layout variant (Events frame changes popup geometry)
            if _vt_qo_girl.events:
                $ _vt_qo_yoffset = 450
            else:
                $ _vt_qo_yoffset = 480

            fixed at vt_appear_after_popup:
                use cherry_window_row(girl=_vt_qo_girl, position="tooltip", xoffset=343, yoffset=_vt_qo_yoffset, border_color="#664444", border_size=2, icon_size=36) id "vt_qo_cherry"

    # Exam classroom
    elif renpy.get_screen("exam_menu", layer="master"):
        use condom_cherry(position="top_left") id "vt_ec_condom"

    elif renpy.get_screen("exam_actions_menu", layer="master"):
        if _vt_girl:
            use condom_cherry(position="top_left") id "vt_ea_condom"
            use cherry_window_row(girl=_vt_girl, position="top", yoffset=100, border_color="#664444", border_size=2, icon_size=36) id "vt_ea_cherry"

    elif renpy.get_screen("exam_outro_screen", layer="master"):
        $ _vt_eo_girls = exam_manager.girls_in_exam.get("player", []) + exam_manager.girls_who_left_early
        for _vt_eo_i, _vt_eo_girl in enumerate(_vt_eo_girls):
            use cherry_window_column(girl=_vt_eo_girl, position="exam_outro", xoffset=76+_vt_eo_i*625, yoffset=120, border_color="#664444", border_size=2, icon_size=25) id "vt_eo_cherry_{}".format(_vt_eo_i)

    elif renpy.get_screen("sex_interaction_menu", layer="master"):
        if _vt_girl:
            use condom_cherry(position="top_right") id "vt_si_condom"
            use cherry_window_row(girl=_vt_girl, position="top_left", border_color="#664444", border_size=2, icon_size=36) id "vt_si_cherry"

    elif renpy.get_screen("sex_outro_screen", layer="master"):
        $ _vt_so_girls = [p for p in getattr(store, "se_participants", []) if isinstance(p, Girl)]
        if _vt_so_girls:
            $ _vt_so_n = len(_vt_so_girls)
            use condom_cherry(position="top") id "vt_so_condom"
            for _vt_so_i, _vt_so_girl in enumerate(_vt_so_girls):
                use cherry_window_row(girl=_vt_so_girl, position="sex_outro", xoffset=-(_vt_so_n-1)*950//2+_vt_so_i*950, yoffset=150, border_color="#664444", border_size=2, icon_size=36) id "vt_so_cherry_{}".format(_vt_so_i)

    elif renpy.get_screen("girl_review_menu", layer="master"):
        if _vt_girl:
            use cherry_window_row(girl=_vt_girl, position="girl_review", border_color="#664444", border_size=2, icon_size=32) id "vt_gr_cherry"

    elif renpy.get_screen("home_visit_call_menu", layer="master"):
        $ _vt_hv_girls = getattr(store, "callable_girls", [])
        $ _vt_hv_per   = getattr(store, "home_visit_per_page", 9)
        $ _vt_hv_page  = getattr(store, "home_visit_page", 0)
        $ _vt_hv_slice = _vt_hv_girls[_vt_hv_page * _vt_hv_per : _vt_hv_page * _vt_hv_per + _vt_hv_per]
        for _vt_hv_i, _vt_hv_girl in enumerate(_vt_hv_slice):
            $ _vt_hv_cx = 22 + (_vt_hv_i % 3) * 630   # card left edge
            $ _vt_hv_cy = 95 + (_vt_hv_i // 3) * 325  # card top edge
            use cherry_window_row(girl=_vt_hv_girl.mother, position="tooltip", xoffset=_vt_hv_cx + 77, yoffset=_vt_hv_cy + 112, border_color="#664444", border_size=1, icon_size=18) id "vt_hv_m_{}".format(_vt_hv_i)
            # Daughter's widget runs as a column down the far-right edge of the card so
            # it clears the base-game status text (grades / grace / "Currently at ...").
            use cherry_window_column(girl=_vt_hv_girl, position="call_menu", xoffset=_vt_hv_cx + 575, yoffset=_vt_hv_cy + 112, border_color="#664444", border_size=1, icon_size=16) id "vt_hv_d_{}".format(_vt_hv_i)

    elif renpy.get_screen("choice") and isinstance(_vt_girl, Girl) and renpy.get_screen("girl_hud"):
        # On the home-visit mother discussion the player is talking to the mother,
        # but selected_girl stays the daughter — show the mother's cherry data there.
        $ _vt_convo_girl = _vt_girl
        if vt_active_label in vt_mother_convo_labels and _vt_girl.mother:
            $ _vt_convo_girl = _vt_girl.mother
        use cherry_window_row(girl=_vt_convo_girl, position="convo_menu", xoffset=17, yoffset=-85, border_color="#664444", border_size=2, icon_size=36) id "vt_choice_cherry"

    if renpy.get_screen("single_girl_rating_menu", layer="master"):
        use vtmod_preg_check_pane() id "vt_preg_pane"

    if renpy.get_screen("player_hud"):
        use hud_condom_cherry(position="top_right", icon_size=32) id "vt_ph_condom"

    if isinstance(_vt_tooltip, Girl):
        $ _tt_mx, _tt_my = renpy.get_mouse_pos()
        # Mirror the base tooltip xpos (screen_tooltip_overlay.rpy) + its frame's +6 padding, so the
        # cherry box stays left-justified with the popup.
        $ _tt_xoffset = max(min(int(_tt_mx), 1920 - max_tooltip_width), 5) + 6
        $ _tt_yoffset = int(1080 * 0.15) + 478
        fixed at tooltip_fade_in:
            use cherry_window_row(girl=_vt_tooltip, position="tooltip", xoffset=_tt_xoffset, yoffset=_tt_yoffset, border_color="#664444", border_size=2, icon_size=36) id "vt_tt_cherry"
